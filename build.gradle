//file:noinspection GroovyUnusedAssignment
plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'eclipse'
    id 'maven-publish'
    //id 'io.github.juuxel.loom-quiltflower' version '1.+'
    id 'org.quiltmc.quilt-mappings-on-loom' version '4.2.+'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = project.mod_version
group = 'com.mrh0.createaddition'
archivesBaseName = "createaddition-fabric+${project.minecraft_version}"

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

loom {
    sourceSets.main.resources { srcDir 'src/generated/resources' }
    accessWidenerPath = file("src/main/resources/ca.accesswidener")
}

repositories {
    maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI

    maven {
        name 'blamejared'
        url 'https://maven.blamejared.com'
    }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://mvn.devos.one/releases/" }
    maven { url = "https://mvn.devos.one/snapshots/" } // Create, Porting Lib, Forge Tags, Milk Lib, Registrate
    maven { url = "https://maven.tterrag.com/" } // Flywheel
    maven { url = "https://jitpack.io/" } // Mixin Extras, Fabric ASM
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup("com.jamieswhiteshirt")
        }
    }
    maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu
    maven { url = "https://maven.cafeteria.dev/releases" }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven { url = "https://dvs1.progwml6.com/files/maven/" } // JEI

    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
    maven { url = "https://api.modrinth.com/maven" } // LazyDFUs
    mavenLocal() // Fast tests
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")

    mappings loom.layered {
        it.officialMojangMappings()
        it.parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}")
    }

    modImplementation("net.fabricmc:fabric-loader:${project.fabric_loader_version}")
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}+${project.minecraft_version}"

    modImplementation("dev.architectury:architectury-fabric:${project.architectury_version}")

    modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cardinal_components_version}")

    // Create - dependencies are added transitively
    modImplementation("com.simibubi.create:create-fabric-${minecraft_version}:${create_version}")

    modImplementation("curse.maven:cc-restitched-462672:3908334")

    modImplementation("appeng:appliedenergistics2-fabric:${project.ae2_version}")

    modImplementation("com.tterrag.registrate_fabric:Registrate:${project.registrate_version}-MC${project.minecraft_version}")

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    include modApi('teamreborn:energy:2.2.0') {
        exclude(group: "net.fabricmc.fabric-api")
    }
    // Development QOL
    modLocalRuntime("maven.modrinth:lazydfu:${lazydfu_version}")
    modLocalRuntime("com.terraformersmc:modmenu:${modmenu_version}")
    // Recipe Viewers - Create Fabric supports JEI, REI, and EMI.
    modImplementation("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")
    modImplementation("mezz.jei:jei-${minecraft_version}-common:${jei_version}")
    modImplementation("me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}")
    modImplementation("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
